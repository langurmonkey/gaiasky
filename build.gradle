import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "java"
    id "com.install4j.gradle" version "10.0.4"
    id "com.dorongold.task-tree" version "1.5"
    id "de.undercouch.download" version "4.1.1"
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    ext {
        appName = "GaiaSky"
        gdxVersion = "1.13.5"
        gdxcontrollersVersion = "2.2.3"
        lwjglVersion = "3.3.3"
        jcommanderVersion = "2.+"
        slf4jVersion = "2.0.+"
        sparkjavaVersion = "2.9.+"
        jafamaVersion = "2.3.+"
        commonsioVersion = "2.+"
        py4jVersion = "0.10.9.+"
        oshiVersion = "6.9.+"
        stilVersion = "4.3"
        jsampVersion = "1.3.+"
        jacksonVersion = "2.20.+"
        ashleyVersion = "1.7.4"
        jtarVersion = "2.+"
        junitVersion = "4.+"
        // Vulnerability fixes
        jettyVersion = "10.0.24"
        jsonVersion = "20231013"
        xmlrpcVersion = "3.0"
    }

    repositories {
        mavenCentral()
        maven { url = 'https://jitpack.io' }
    }
}

project(":core") {
    apply plugin: "java-library"

    def cmd = "git describe --abbrev=0 --tags HEAD"
    def proc = cmd.execute()
    project.ext.tag = proc.text.trim()

    cmd = "git rev-parse --short HEAD"
    proc = cmd.execute()
    project.ext.rev = proc.text.trim()

    if (DefaultNativePlatform.currentOperatingSystem.windows) {
        project.ext.system = DefaultNativePlatform.currentOperatingSystem.getName() + " " + DefaultNativePlatform.currentArchitecture.getName()
    } else {
        cmd = "uname -snmr"
        proc = cmd.execute()
        project.ext.system = proc.text.trim()
    }

    project.ext.builder = System.properties["user.name"]
    project.ext.buildtime = new Date()
    version = "$tag"

    println ""
    println "CURRENT SYSTEM"
    println "=============="
    println "java version: " + JavaVersion.current().toString()
    println "system: $system"

    println ""
    println "GAIA SKY"
    println "========"
    println "git tag: $tag"
    println "git rev: $rev"
    println "buildtime: $buildtime"
    println "builder: $builder"
    println ""

    // Set some build variables
    project.ext.baseDir = System.getProperty("user.dir")
    project.ext.tagRev = project.tag + "." + project.rev
    project.ext.distName = "gaiasky-$tagRev"
    project.ext.releasesDir = "$baseDir/releases"
    project.ext.distDir = "$releasesDir/$distName"
    project.ext.packageName = "packages-$tagRev"
    project.ext.packageDir = "$releasesDir/$packageName"

    println ""
    println "BUILD VARIABLES AND INFO"
    println "========================"
    println "base dir: $baseDir"
    println "tag.rev: $tagRev"
    println "dist name: $distName"
    println "dist dir: $distDir"
    println "packages name: $packageName"
    println "packages dir: $packageDir"
    println ""

    tasks.withType(JavaCompile).configureEach { options.compilerArgs << "-parameters" }

    dependencies {
        // *****************************
        // GENERATE SKINS (PackUITextures)
        // *****************************
        compileOnly group: "com.badlogicgames.gdx", name: "gdx-tools", version: "$gdxVersion"

        // ************
        // REGULAR DEPS
        // ************

        // OpenXR
        implementation group: "org.lwjgl", name: "lwjgl-openxr", version: "$lwjglVersion"
        implementation group: "org.lwjgl", name: "lwjgl-glfw", version: "$lwjglVersion"
        runtimeOnly "org.lwjgl:lwjgl-openxr:$lwjglVersion:natives-linux"
        runtimeOnly "org.lwjgl:lwjgl-openxr:$lwjglVersion:natives-windows"

        // LibGDX
        implementation group: "com.badlogicgames.gdx", name: "gdx", version: "$gdxVersion"
        implementation group: "com.badlogicgames.gdx", name: "gdx-backend-lwjgl3", version: "$gdxVersion"
        runtimeOnly "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

        // gdx-controllers:2.+ bases up on SDL 2.0.12
        implementation group: "com.badlogicgames.gdx-controllers", name: "gdx-controllers-core", version: "$gdxcontrollersVersion"
        implementation group: "com.badlogicgames.gdx-controllers", name: "gdx-controllers-desktop", version: "$gdxcontrollersVersion"

        // Ashley
        implementation group: "com.badlogicgames.ashley", name: "ashley", version: "$ashleyVersion"

        // STIL library to load datasets
        implementation group: "uk.ac.starlink", name: "stil", version: "$stilVersion"
        // JSAMP for SAMP communication
        implementation group: "uk.ac.starlink", name: "jsamp", version: "$jsampVersion"

        // File utilities
        implementation group: "commons-io", name: "commons-io", version: "$commonsioVersion"
        // Command line arguments
        implementation group: "org.jcommander", name: "jcommander", version: "$jcommanderVersion"
        // Fast math
        implementation group: "net.jafama", name: "jafama", version: "$jafamaVersion"
        // Tar
        implementation group: "org.kamranzafar", name: "jtar", version: "$jtarVersion"

        // JPEGXL
        // implementation group: "com.github.umjammer", name: "vavi-image-jpegxl", version: "0.0.7"

        // Scripting
        implementation group: "net.sf.py4j", name: "py4j", version: "$py4jVersion"
        // System information
        implementation group: "com.github.oshi", name: "oshi-core-java11", version: "$oshiVersion"
        // Yaml
        implementation group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "$jacksonVersion"
        implementation group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-yaml", version: "$jacksonVersion"
        implementation group: "com.fasterxml.jackson.datatype", name: "jackson-datatype-jsr310", version: "$jacksonVersion"

        // ****************
        // REST SERVER DEPS
        // ****************
        implementation group: "org.slf4j", name: "slf4j-nop", version: "$slf4jVersion"
        implementation group: "com.sparkjava", name: "spark-core", version: "$sparkjavaVersion"

        // *******************
        // VULNERABILITY FIXES
        // *******************
        // CVE-2023-26049
        implementation group: "org.eclipse.jetty", name: "jetty-xml", version: "$jettyVersion"
        implementation group: "org.eclipse.jetty", name: "jetty-http", version: "$jettyVersion"
        implementation group: "org.eclipse.jetty", name: "jetty-server", version: "$jettyVersion"
        // CVE-2022-45690
        implementation group: "org.json", name: "json", version: "$jsonVersion"
        // CVE-2016-5002
        implementation group: "org.apache.xmlrpc", name: "xmlrpc", version: "$xmlrpcVersion"

        // *************
        // INTERNAL DEPS
        // *************
        implementation files("../assets")

        // *********
        // TEST DEPS
        // *********
        testImplementation group: "junit", name: "junit", version: "$junitVersion"
        testRuntimeOnly group: "org.junit.platform", name: "junit-platform-launcher", version: "1.+"

        // **********
        // DEBUG DEPS
        // **********
        //implementation group: "org.openjdk.jol", name: "jol-core", version: "+"
    }

    jar {
        manifest {
            attributes "Implementation-Title": "Gaia Sky",
                    "Implementation-Version": archiveVersion,
                    "Main-Class": "gaiasky.desktop.GaiaSkyDesktop"
        }
        from("../assets") {
            include "font/font-distance-field.fnt"
            include "font/font-distance-field.png"
            include "icon/gs_icon_256.png"
            include "icon/gs_round_256.png"
            include "icon/gsvr_round_256.png"
            include "icon/gsascii.txt"
            include "img/**"
            include "text/**"
            include "shader/**"
            include "archetypes/**"
            include "data/**"
            include "shaders/**"
            include "skins/**"
            exclude "skins/raw"
            exclude "assets-bak/**"
        }
        from("build/classes/main") { include "**" }
    }

}
