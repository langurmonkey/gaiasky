import org.apache.tools.ant.taskdefs.condition.Os 

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        flatDir { dirs "lib" }
    }
    dependencies {
        classpath "de.richsource.gradle.plugins:gwt-gradle-plugin:0.6"
        classpath "gradle.plugin.install4j.install4j:gradle_plugin:6.1.2"
    }
}

plugins {
  id "com.install4j.gradle" version "7.0.8"
  id "com.dorongold.task-tree" version "1.3.1"
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '2.1.7'

    ext {
        appName = "GaiaSky"
        gdxVersion = "1.9.9"
        roboVMVersion = "1.4.0"
        box2DLightsVersion = "1.3"
        ashleyVersion = "1.4.0"
        aiVersion = "1.5.0"
        lwjglVersion = "3.2.0"
        jcommanderVersion = '1.72'
        googleanalyticsjavaVersion = "1.1.2"
        slf4jVersion = "1.7.25"
        sparkjavaVersion = "2.7.2"
        gsonVersion = "2.8.0"
        jafamaVersion = "2.1.0"
        compressVersion = "1.18"
        commonsioVersion = '2.6'
        py4jVersion = '0.10.8.1'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://mvnrepository.com/artifact/xmlrpc/xmlrpc" }
    }

    eclipse {
        classpath { downloadSources=true }
    }
}

project(":core") {
    apply plugin: "java"

    def cmd = "git describe --abbrev=0 --tags HEAD"
    def proc = cmd.execute()
    project.ext.tag = proc.text.trim()

    cmd = "git rev-parse --short HEAD"
    proc = cmd.execute()
    project.ext.rev = proc.text.trim()

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        project.ext.system = "Windows10 x86_64"
    }else {
        cmd = "uname -snmr"
        proc = cmd.execute()
        project.ext.system = proc.text.trim()
    }

    cmd = "whoami"
    proc = cmd.execute()
    project.ext.builder = proc.text.trim()

    project.ext.buildtime = new Date()

    version = "$tag"

    println ""
    println "CURRENT SYSTEM"
    println "=============="
    println "java version: " + JavaVersion.current().toString()
    println "system: $system"

    println ""
    println "GAIA SKY"
    println "========"
    println "git tag: $tag"
    println "git rev: $rev"
    println "buildtime: $buildtime"
    println "builder: $builder"
    println ""


    // Set some build variables
    project.ext.baseDir = System.getProperty("user.dir")
    project.ext.tagRev = project.tag + "." + project.rev
    project.ext.distName = "gaiasky-$tagRev"
    project.ext.releasesDir = "$baseDir/releases"
    project.ext.distDir = "$releasesDir/$distName"
    project.ext.packageName = "packages-$tagRev"
    project.ext.packageDir = "$releasesDir/$packageName"

    println ""
    println "BUILD VARIABLES AND INFO"
    println "========================"
    println "base dir: $baseDir"
    println "tag.rev: $tagRev"
    println "dist name: $distName"
    println "dist dir: $distDir"
    println "packages name: $packageName"
    println "packages dir: $packageDir"
    println ""

    tasks.withType(JavaCompile) { options.compilerArgs << "-parameters" }

    dependencies {
        // *****************************
        // GENERATE SKINS (PackTextures)
        // *****************************
        //compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"


        // ************
        // REGULAR DEPS
        // ************
        compile group: "org.lwjgl", name: "lwjgl", version: "$lwjglVersion"
        compile group: "org.lwjgl", name: "lwjgl-glfw", version: "$lwjglVersion"
        compile group: "org.lwjgl", name: "lwjgl-jemalloc", version: "$lwjglVersion"
        compile group: "org.lwjgl", name: "lwjgl-opengl", version: "$lwjglVersion"
        compile group: "org.lwjgl", name: "lwjgl-openal", version: "$lwjglVersion"
        
        compile group: "org.lwjgl", name: "lwjgl-openvr", version: "$lwjglVersion"
        compile "org.lwjgl:lwjgl-openvr:$lwjglVersion:natives-linux"
        compile "org.lwjgl:lwjgl-openvr:$lwjglVersion:natives-windows"
        compile "org.lwjgl:lwjgl-openvr:$lwjglVersion:natives-macos"
        
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers-lwjgl3:$gdxVersion"

        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile group: "commons-io", name: "commons-io", version: "$commonsioVersion"
        compile "org.apache.commons:commons-compress:$compressVersion"
        compile "com.beust:jcommander:$jcommanderVersion"
        compile "com.brsanthu:google-analytics-java:$googleanalyticsjavaVersion"
        compile "net.jafama:jafama:$jafamaVersion"
        compile group: "net.sf.py4j", name: "py4j", version: "$py4jVersion"



        // ****************
        // REST SERVER DEPS
        // ****************
        //compile "org.slf4j:slf4j-nop:$slf4jVersion"
        compile "org.slf4j:slf4j-simple:$slf4jVersion"
        compile "com.sparkjava:spark-core:$sparkjavaVersion"
        compile "com.google.code.gson:gson:$gsonVersion"
        compile "junit:junit:4.12"


        // *************
        // INTERNAL DEPS
        // *************
        compile files("lib/postprocessing.jar",
                "lib/stil.jar",
                "lib/jsamp-1.3.5.jar",
                "../assets")
    }

    jar {
        manifest {
            attributes "Implementation-Title": "Gaia Sky VR",
            "Implementation-Version": version
        }
        from("../assets") {
            include "font/main-font.fnt"
            include "font/main-font.png"
            include "font/font2d.fnt"
            include "font/font2d.png"
            include "font/font-titles.fnt"
            include "font/font-titles.png"
            include "icon/ic_launcher.png"
            include "img/**"
            exclude "img/splash"
            include "shader/**"
            include "shaders/**"
            include "skins/**"
            exclude "skins/raw"
            exclude "assets-bak/**"
        }
        from("build/classes/main") { include "**" }
    }

}

tasks.eclipse.doLast { delete ".project" }


task createWrapper(type: Wrapper) { gradleVersion = "4.4.1" }
